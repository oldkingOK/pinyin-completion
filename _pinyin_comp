# [:ascii:] need
setopt rematchpcre
local -A _punctuation_map=(
  [～]=~
  [！]=!
  [＠]=@
  [＃]='#'
  [＄]=$
  [％]=%
  [＆]='&'
  [＊]=*
  [（]='('
  [）]=')'
  [＿]=_
  [－]=-
  [＋]=+
  [［]='['
  [］]=']'
  [「]='['
  [」]=']'
  [【]='['
  [】]=']'
  [〔]='['
  [〕]=']'
  [＜]='<'
  [＞]='>'
  [《]='<'
  [》]='>'
  [«]='<'
  [»]='>'
  [‹]='<'
  [›]='>'
  [？]=?
  [，]=,
  [。]=.
  [／]=/
  [＼]=\\
  [、]=\\
  […]=...
)

autoload -Uz pinyin-convert

# unix file names can contain '\n', so use '\0' to separate them
local IFS=$'\0' suffix='' file result k v
local -a files
if [ "$words[1]" = cd ] ; then
  suffix=/
fi
# print -N use '\0' to separate outputs
for file in $(print -nN ${1:h}/*"$suffix"); do
  file="${file#./}"
  result="$file"
  if [[ $result =~ [^[:ascii:]] ]]; then
    for k v in ${(kv)FUZZY} ${(kv)_punctuation_map}; do
      result="${result//$k/$v}"
    done
    pinyin-convert $result
    result=$reply
  fi
  if [[ $result == $1* ]]; then
    files+=(${(q)file})
  fi
done
reply=($files)

# ex: filetype=zsh
